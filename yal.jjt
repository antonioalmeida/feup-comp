

options
{
  LOOKAHEAD= 1;
  MULTI = true;
}

PARSER_BEGIN(Yal)
import java.io.File;
import java.io.FileInputStream;
import java.io.FileNotFoundException;

import java.io.FileOutputStream;
import java.io.IOException;
import java.util.Arrays;




public class Yal
{
		
  private static Yal myYal;
  
  public static void main(String args []) throws ParseException, IOException
  {
    
    if( validArgs(args) == false)
    	return;
    else { 
    	SimpleNode root = myYal.Program(); // devolve referência para o nó raiz da árvore
     
      	System.out.println("\nAST Tree:");
    	root.dump(""); // imprime no ecrã a árvore
    	 root.analyse();
		System.out.println("\nSymbol Table:");
		root.printSymbolTable("");

		//generateCode(root);
		CodeGenerator codeGenerator = new CodeGenerator(root, "test.j");
		codeGenerator.generateCode();
    }
  }

  public static void generateCode(SimpleNode root) throws IOException
  {
    FileOutputStream outputStream = new FileOutputStream("test.j");
    String codeString = root.generateCode();
    byte [] strToBytes = codeString.getBytes();
    outputStream.write(strToBytes);
    outputStream.close();
  }

  public static File validFilePath(String filePath) {
		File file = new File(filePath);
		
		if(file.exists())
			return file;		
		else
			return null;
			
  }
  	
  public static boolean validArgs(String args []) {

		if(args.length > 1) { 
			System.out.println("Invalid number of arguments");
			return false;
		}
		
		else if(args.length == 0)
			myYal = new Yal(System.in);

		else if(args.length == 1) {
                        File file;
                        if( (file = validFilePath(args[0])) == null) {
                                System.out.println("Invalid file Path");
                                return false;
                        }
                        else {
                                FileInputStream stream;
								try {
									stream = new FileInputStream(file);
									myYal = new Yal(stream);
								} catch (FileNotFoundException e) {
									System.out.println("Error in stream constructor: ");
									e.printStackTrace();
									return false;
								}
                                
                        }
        }
		

		return true;
  }

 
  	
  public static void errorSkipTo(ParseException e, String errorMessage,  SkipToken... skipTargets) {
    System.out.println("Syntatic Error on " + errorMessage + ". " + e.toString());
    Token t1;
    boolean foundTarget = false;

	 //System.out.println(token);
	
    do {
      t1 = getToken(1);

	  for(int i = 0; i < skipTargets.length && foundTarget == false; i++)
	  	 if(skipTargets[i].getToken() == t1.kind) {
	  	 	foundTarget = true;
	  	 	if(skipTargets[i].isConsumed() == true)
	  	 	  getNextToken();

	  	 	/*else {
	  	 	  System.out.println(t1); 
	  	 	  System.out.println(skipTargets[i].token);	  	 	 
	  	 	 
	  	 	}*/
	  	 }
	  
     

      if(!foundTarget)
      	getNextToken();
      	
    } while (!foundTarget);

     //System.out.println(token);
  }

  public static void coverageBlock() throws ParseException {
	Token t1;
    t1 = getToken(1);
    if(t1.kind == LCHAVETA) {
	  getNextToken();
      StmtlstError();
    }
  }

  public static void consumeToken(Integer... tokens) throws ParseException {
  	Token t;
  	t = getToken(1);
	
  	if(Arrays.asList(tokens).contains(t.kind)) {
  	    System.out.println("contains");
  		getNextToken();
  	}
  
  	  
  }
}

PARSER_END(Yal)

< DEFAULT >
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| < "//" (~[ "\n", "\r" ])*
    (
      "\n"
    | "\r"
    | "\r\n"
    ) >
| < "/*" (~[ "*" ])* "*"
    (
      "*"
    | ~[ "*", "/" ] (~[ "*" ])* "*"
    )*
    "/" >
}

/* reserved words */
< DEFAULT >
TOKEN :
{
  < RELA_OP :
    ">"
  | "<"
  | "<="
  | ">="
  | "=="
  | "!=" >
| < ADDSUB_OP :
    "+"
  | "-" >
| < ARITH_OP :
    "*"
  | "/"
  | "<<"
  | ">>"
  | ">>>" >
| < BITWISE_OP :
    "&"
  | "|"
  | "^" >
| < NOT_OP : "!" >
| < WHILE : "while" >
| < IF : "if" >
| < ELSE : "else" >
| < ASSIGN : "=" >
| < ASPA : "\"" >
| < LPAR : "(" >
| < RPAR : ")" >
| < VIRG : "," >
| < PVIRG : ";" >
| < LCHAVETA : "{" >
| < RCHAVETA : "}" >
| < FUNCTION : "function" >
| < MODULE : "module" >
| < SIZE : "size" >
}

< DEFAULT >
TOKEN :
{
  < INTEGER : (< DIGIT >)+ >
| < ID :
    < LETTER >
    (
      < LETTER >
    | < DIGIT >
    )* >
| < #LETTER : [ "$", "A"-"Z", "_", "a"-"z" ] >
| < #DIGIT : [ "0"-"9" ] >
| < STRING : "\"" ([ "a"-"z", "A"-"Z", "0"-"9", ":", " ", "=" ])+ "\"" >
}

SimpleNode Program() :
{}
{
  Module()
  {
    return jjtThis;
  }
}

void Module() : 
{Token t;}
{
  try { 
  < MODULE > t=< ID >{jjtThis.jjtAddValue(t.image);} < LCHAVETA > }catch(ParseException e) {
    System.out.println("Syntatic error on module declaration. " + e.toString());
    Token t1;
    return;    
  }
  try { 
  (
    Declaration()
  )* }catch(ParseException e) {
		errorSkipTo(e, "declarations module area", new SkipToken(PVIRG, true));
     }
  try { 
  (
    Function()
  )*
  < RCHAVETA > } catch(ParseException e) {
    errorSkipTo(e, "functions module area", new SkipToken(RCHAVETA, true));
  }
}

void Declaration() :
{}
{
  (
    Element()
  )
  (
    < ASSIGN >
    (
      ArrayAssigned()
    | ScalarAssigned() 
    )
  )?
  < PVIRG >
}

void ArrayAssigned() : { }
{
  "[" ArraySize() "]"
}

void ScalarAssigned() : {Token t1, t2; }
{
	(	  t1 = < ADDSUB_OP > { jjtThis.jjtAddValue(t1.image);})?
	t2 = < INTEGER > { jjtThis.jjtAddValue(t2.image);}
}



void Function() : { Token t=null;}
{ 
  try {
    < FUNCTION > t=< ID >{jjtThis.jjtAddValue(t.image);} }catch(ParseException e) {
			errorSkipTo(e, "function name", new SkipToken(LPAR, false));
       }
    try { 
    (
      (
        (
          ArrayElementNoID(t.image)
        | ScalarElementNoID(t.image)
        )
       	FunctionAssign()
        < LPAR >
        (
          Varlist()
        )?
        < RPAR >
      )
    |
      (
  	 < LPAR >
        (
          Varlist()
        )?
        < RPAR >
      )
    )
  } 
  catch(ParseException e) {
    errorSkipTo(e, "function declaration", new SkipToken(RPAR, true));
  }
  try { 
  < LCHAVETA > Stmtlst() < RCHAVETA > } catch(ParseException e) {
		errorSkipTo(e, "function body",  new SkipToken(RCHAVETA, true));
    }
}

void FunctionAssign() : {Token t;}
{
	
	 < ASSIGN > t=< ID > 
	 {jjtThis.jjtAddValue(t.image);}
}

void Varlist() : { Token t1, t2; }
{ t1 = < ID >
  (
    ArrayElementNoID(t1.image)
  | ScalarElementNoID(t1.image)
  )
  (
    < VIRG >  t2 = < ID >
    (
     ArrayElementNoID(t2.image)
    | ScalarElementNoID(t2.image)
    )
  )*
}

void Element() #void: {Token t;}{
	t=<ID> (
		ArrayElementNoID(t.image)
	  | ScalarElementNoID(t.image)
	)
	//{jjtThis.jjtAddValue(t.image);}
}

void ArrayElementNoID(String id) #ArrayElement:
{}
{
	{jjtThis.jjtAddValue(id);}
  "[" "]"
}


void ScalarElementNoID(String id) #ScalarElement:
{}
{
	{jjtThis.jjtAddValue(id);}
}

void ArrayElement() :
{}
{
  < ID > "[" "]"
}

void ScalarElement() :
{}
{
  < ID >
}

void Stmtlst() #void:
{}
{
  (
    Stmt()
  )*
}

void StmtlstError() #void :
{}
{
  (
    Stmt()
  )*"}"
}

void Stmt() #void: { Token t; }
{
  try { 
    While()
  | If() 
  |(
    t = < ID >
  ( 
     AssignNoPoint(t.image, false)
  | CallVirg(t.image) |  "."NoPointAssignCallVirg(t.image))) 
  } 
  catch(ParseException e) {
  	System.out.println("Syntatic error during Statement declaration. " + e.toString());
      Token t2;

      do {
        t2 = getNextToken();

        /*
        if(t2.kind == LCHAVETA) {
          StmtlstError();
          return;
        }*/
      } while(t2.kind != PVIRG);
    }
}

void NoPointAssignCallVirg(String id) #void:
{}
{
  (
    < ID > CallVirg(id)) | (< SIZE > AssignNoPoint(id, true))
}

void Assign() :
{}
{
  Lhs() < ASSIGN > Rhs() < PVIRG >
}

void AssignNoID(String id) #Assign:
{ }
{
  
  try {
    LhsNoID(id) < ASSIGN > Rhs() < PVIRG >
  }
  catch(ParseException e) {
    errorSkipTo(e, "assignment", new SkipToken( PVIRG, true));
  }
}

void AssignNoPoint(String id, boolean size_array) #Assign:
{ }
{
  
  try {
    LhsNoPoint(id, size_array) < ASSIGN > Rhs() < PVIRG >
  }
  catch(ParseException e) {
    errorSkipTo(e, "assignment",  new SkipToken(PVIRG, true));
  }
}
  

void Lhs() #void:
{Token t;}
{
  t = < ID >(
  ArrayAccessNoID(t.image)
| ScalarAccessNoID(t.image))
}

void LhsNoID(String id) #void:
{ }
{
  /*{jjtThis.jjtAddValue(id);}*/
  ArrayAccessNoID(id)
| ScalarAccessNoID(id)
}
  
void LhsNoPoint(String id, boolean size_array) #void:
{ }
{
  ArrayAccessNoID(id)
| ScalarAccessNothing(id, size_array)
}
  

void Rhs():
{Token t=null;}
{ { }
  (
    Term()
    (
      (
        t=< ARITH_OP >
      | t=< BITWISE_OP >
      | t=< ADDSUB_OP >
      )
      {jjtThis.jjtAddValue(t.image);}
      Term()
    )?
  )
| ArrayAssigned()


}

void ArraySize() :
{Token t;}
{
  ScalarAccess()
| t=< INTEGER >{jjtThis.jjtAddValue(t.image);}
}

void Term() :
{
  Token t,t2=null;
}
{
  (t2=< ADDSUB_OP >{jjtThis.jjtAddValue(t2.image);})?
  (
    t=< INTEGER >{jjtThis.jjtAddValue(t.image); }
  |
    ( 
      t = < ID > 
      (Call(t.image) | "." PointCallScalarAccess(t.image) | ArrayAccessNoID(t.image) 
		| ScalarAccessNothing(t.image, false))  
        
    )
  )
  
}



void Exprtest() :
{Token t;}
{
  < LPAR > Lhs() t=< RELA_OP > Rhs() < RPAR >
  {jjtThis.jjtAddValue(t.image);}
}

void While():
{}
{ 
    try {
      < WHILE > Exprtest() < LCHAVETA > 
    }
    catch(ParseException e) {
      errorSkipTo(e, "WHILE statement declaration",  new SkipToken(LCHAVETA, true));
    }
    Stmtlst() < RCHAVETA >
}

void If() :
{}
{
  try {
    < IF > Exprtest() < LCHAVETA > Stmtlst() < RCHAVETA >
  }
  catch (ParseException e) {
    errorSkipTo(e, "IF statement",  new SkipToken(RCHAVETA, true));
  }

  try {
    (
      < ELSE > < LCHAVETA >
      Stmtlst() < RCHAVETA >
    )?
  }
  catch (ParseException e) {
    System.out.println("Syntatic error on ELSE statement. " + e.toString());
    Token t1;
    boolean hasBrackets = false;
    // If left brackets is found, skip to right bracket
    // otherwise, skip to ';'

    // TODO: Refactor this later
    do {
      t1 = getNextToken();
      if(t1.kind == LCHAVETA) {
        hasBrackets = true;
        break;
      }
    } while (t1.kind != PVIRG);

    if(hasBrackets) {
      do {
        t1 = getNextToken();
      } while (t1.kind != RCHAVETA);
    }
  }
}


void Call (String id) #Call:
{Token t=null; }
{
  try {
    < LPAR > }catch(ParseException e) {
		errorSkipTo(e, ": function call must have parentheses", new SkipToken(RPAR, false), new SkipToken(PVIRG, true));
		return;
      }
    try {
		(
		  ArgumentList()
		)?
		<  RPAR >
      }catch(ParseException e) {
        errorSkipTo(e, "function call", new SkipToken(RPAR, true));
      }
  {jjtThis.jjtAddValue(id);}
	
}
  
   
void CallVirg (String id) #Call:
{Token t=null; }
{
  try {
    < LPAR > }catch(ParseException e) {
		errorSkipTo(e, ": function call must have parentheses", new SkipToken(RPAR, false), new SkipToken(PVIRG, true));
		return;
      }
    try {
		(
		  ArgumentList()
		)?
		<  RPAR > < PVIRG >
      }catch(ParseException e) {
        errorSkipTo(e, "function call", new SkipToken(RPAR, true));
        coverageBlock();
        consumeToken(PVIRG);
      }
  {jjtThis.jjtAddValue(id);}
	
}

void ArgumentList() #void:
{}
{
  Argument()
  (
    < VIRG > Argument()
  )*
}

void Argument() :
{Token t;}
{
  (
    t=< ID >
  | t=< STRING > 
  | t=< INTEGER > 
  )
  {jjtThis.jjtAddValue(t.image);}
}

void ArrayAccess() :
{}
{
  < ID > "[" Index() "]"
}

void ArrayAccessNoID(String id) #ArrayAccess:
{ }
{
  {jjtThis.jjtAddValue(id); }
  "[" Index() "]"
}

void ScalarAccess() #void:
{Token t;}
{
  t=< ID > ScalarAccessNoID(t.image)
}

void ScalarAccessNoID(String id) #ScalarAccess:
{ Token t=null;}
{
  {jjtThis.jjtAddValue(id); }
  ("." < SIZE >{jjtThis.set_size_access(true);})?

}

void ScalarAccessNothing(String id, boolean size_array) #ScalarAccess:
{ Token t=null; }
{
  {jjtThis.jjtAddValue(id); jjtThis.set_size_access(size_array);}
    
} 

void PointCallScalarAccess(String id) #void:
{
  
}
{
  (
    < ID >  Call(id)) | (< SIZE > ScalarAccessNothing(id, true))
}



void Index() :
{Token t;}
{
( t=< ID >
| t=< INTEGER >)
{jjtThis.jjtAddValue(t.image); }
}


  
