options
{
  LOOKAHEAD= 1;

}

PARSER_BEGIN(Yal)
public class Yal
{
  public static void main(String args []) throws ParseException
  {
    Yal myYal = new Yal(System.in);
    SimpleNode root = myYal.Program(); // devolve referência para o nó raiz da árvore
    root.dump(""); // imprime no ecrã a árvore
  }
}

PARSER_END(Yal)

< DEFAULT >
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| < "//" (~[ "\n", "\r" ])*
    (
      "\n"
    | "\r"
    | "\r\n"
    ) >
| < "/*" (~[ "*" ])* "*"
    (
      "*"
    | ~[ "*", "/" ] (~[ "*" ])* "*"
    )*
    "/" >
}

/* reserved words */
< DEFAULT >
TOKEN :
{
  < RELA_OP :
    ">"
  | "<"
  | "<="
  | ">="
  | "=="
  | "!=" >
| < ADDSUB_OP :
    "+"
  | "-" >
| < ARITH_OP :
    "*"
  | "/"
  | "<<"
  | ">>"
  | ">>>" >
| < BITWISE_OP :
    "&"
  | "|"
  | "^" >
| < NOT_OP : "!" >
| < WHILE : "while" >
| < IF : "if" >
| < ELSE : "else" >
| < ASSIGN : "=" >
| < ASPA : "\"" >
| < LPAR : "(" >
| < RPAR : ")" >
| < VIRG : "," >
| < PVIRG : ";" >
| < LCHAVETA : "{" >
| < RCHAVETA : "}" >
| < FUNCTION : "function" >
| < MODULE : "module" >
| < SIZE : "size" >
}

< DEFAULT >
TOKEN :
{
  < INTEGER : (< DIGIT >)+ >
| < ID :
    < LETTER >
    (
      < LETTER >
    | < DIGIT >
    )* >
| < #LETTER : [ "$", "A"-"Z", "_", "a"-"z" ] >
| < #DIGIT : [ "0"-"9" ] >
| < STRING : "\"" ([ "a"-"z", "A"-"Z", "0"-"9", ":", " ", "=" ])+ "\"" >
}

SimpleNode Program() :
{}
{
  Module()
  {
    return jjtThis;
  }
}

void Module() : 
{Token t;}
{
  < MODULE > t=< ID >{jjtThis.jjtSetValue(t.image);} < LCHAVETA >
  (
    Declaration()
  )*
  (
    Function()
  )*
  < RCHAVETA >
}

void Declaration() :
{}
{
  (
    Element()
  )
  (
    < ASSIGN >
    (
      (
        "[" ArraySize() "]"
      )
    | (< ADDSUB_OP >)? < INTEGER >
    )
  )?
  < PVIRG >
}

void Function() : { Token t;}
{ 
  try {
    < FUNCTION > t=< ID >
    (
      (
        (
          (
            ArrayElementNoID(t.image))?
        /*| ScalarElementNoID(t)*/
        )
        < ASSIGN > < ID > 
        < LPAR >
        (
          Varlist()
        )?
        < RPAR >
      )
    |
      (
  	 < LPAR >
        (
          Varlist()
        )?
        < RPAR >
      )
    )
  } 
  catch(ParseException e) {
    System.out.println("Syntatic error on function declaration. " + e.toString());
    Token t1;
    do {
      t1 = getNextToken();
    } while (t1.kind != RPAR);
  }
  < LCHAVETA > Stmtlst() < RCHAVETA >
}

void Varlist() : { Token t1, t2; }
{ t1 = < ID >
  (
    (
      ArrayElementNoID(t1.image))?
  /*| ScalarElementNoID(t)*/
  )
  (
    < VIRG >  t2 = < ID >
    (
      (
        ArrayElementNoID(t2.image))?
    /*| ScalarElementNoID(t2) We don't know if we are supposed to have scalarElement*/
    )
  )*
}

void Element() #void:{Token t;}{
	t=<ID> (
		(
		  ArrayElementNoID(t.image))?
	 /* | ScalarElementNoID(t.image)*/
	)
}

void ArrayElementNoID(String id) #ArrayElement:
{}
{
	{jjtThis.jjtSetValue(id);}
  "[" "]"
}

/*
TODO: Ask teacher if it is preferable to have a warning or to delete ScalarElement
void ScalarElementNoID(String id) #ScalarElement:
{}
{
	{jjtThis.setValue(id);}
  	""
}*/

void ArrayElement() :
{}
{
  < ID > "[" "]"
}

void ScalarElement() :
{}
{
  < ID >
}

void Stmtlst() :
{}
{
  (
    Stmt()
  )*
}


void Stmt() : { Token t; }
{
  
  While()
| If() 
|(
  t = < ID >
( LOOKAHEAD(2)
   AssignNoID(t.image)
| CallNoID(t.image) < PVIRG >))
}

void Assign() :
{}
{
  Lhs() < ASSIGN > Rhs() < PVIRG >
}

void AssignNoID(String id) #Assign:
{ }
{
  {jjtThis.jjtSetValue(id);}

  try {
    LhsNoID(id) < ASSIGN > Rhs() < PVIRG >
  }
  catch(ParseException e) {
    /* Error during assignment - skipping to next ';' */
    System.out.println("Syntatic error during assignment. " + e.toString());
    Token t;
    do {
      t = getNextToken();
    } while (t.kind != PVIRG);
  }

}

void Lhs() :
{Token t;}
{
  t = < ID >(
  ArrayAccessNoID(t.image)
| ScalarAccessNoID(t.image))
}

void LhsNoID(String id) #Lhs:
{ }
{
  {jjtThis.jjtSetValue(id);}
  ArrayAccessNoID(id)
| ScalarAccessNoID(id)
}
  

void Rhs() :
{}
{
  (
    Term()
    (
      (
        < ARITH_OP >
      | < BITWISE_OP >
      | < ADDSUB_OP >
      )
      Term()
    )?
  )
| "[" ArraySize() "]"
}

void ArraySize() :
{}
{
  ScalarAccess()
| < INTEGER >
}

void Term() :
{
  Token t;
}
{
  (< ADDSUB_OP >)?
  (
    < INTEGER >
  |
    (
      t = < ID >
      ( LOOKAHEAD(2)
        CallNoID(t.image)
      | ArrayAccessNoID(t.image)
      | ScalarAccessNoID(t.image)
      )
    )
  )
}

void Exprtest() :
{}
{
  < LPAR > Lhs() < RELA_OP > Rhs() < RPAR >
}

void While() :
{}
{
    try {
      < WHILE > Exprtest() < LCHAVETA > Stmtlst() < RCHAVETA >
     }
    catch(ParseException e) {
      System.out.println("Syntatic error on WHILE statement. " + e.toString());
      Token t;
      do {
        t = getNextToken();
      } while (t.kind != RCHAVETA);
    }
}

void If() :
{}
{
  try {
    < IF > Exprtest() < LCHAVETA > Stmtlst() < RCHAVETA >
  }
  catch (ParseException e) {
    System.out.println("Syntatic error on IF statement. " + e.toString());
    Token t;
    do {
      t = getNextToken();
    } while (t.kind != RCHAVETA);
  }

  try {
    (
      < ELSE > < LCHAVETA >
      Stmtlst() < RCHAVETA >
    )?
  }
  catch (ParseException e) {
    System.out.println("Syntatic error on ELSE statement. " + e.toString());
    Token t1;
    boolean hasBrackets = false;
    // If left brackets is found, skip to right bracket
    // otherwise, skip to ';'

    // TODO: Refactor this later
    do {
      t1 = getNextToken();
      if(t1.kind == LCHAVETA) {
        hasBrackets = true;
        break;
      }
    } while (t1.kind != PVIRG);

    if(hasBrackets) {
      do {
        t1 = getNextToken();
      } while (t1.kind != RCHAVETA);
    }
  }
}

void Call() :
{}
{
  < ID > ("." < ID >)? < LPAR >
  (
    ArgumentList()
  )?
  < RPAR >
}

void CallNoID(String id) #Call:
{ }
{
  {jjtThis.jjtSetValue(id);}
  ("." < ID >)? < LPAR >
  (
    ArgumentList()
  )?
  < RPAR >
}
  

void ArgumentList() :
{}
{
  Argument()
  (
    < VIRG > Argument()
  )*
}

void Argument() :
{}
{
  (
    < ID >
  | < STRING >
  | < INTEGER >
  )
}

void ArrayAccess() :
{}
{
  < ID > "[" Index() "]"
}

void ArrayAccessNoID(String id) #ArrayAccess:
{ }
{
  {jjtThis.jjtSetValue(id); }
  "[" Index() "]"
}

void ScalarAccess() :
{}
{
  < ID > ("." < SIZE >)?
}

void ScalarAccessNoID(String id) #ScalarAccess:
{ }
{
  {jjtThis.jjtSetValue(id); }
  ("." < SIZE >)?
}

void Index() :
{}
{
  < ID >
| < INTEGER >
}
